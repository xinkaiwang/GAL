GAL22V10B
A EXAMPLE for clock logic
2020-02-25, by xinkai
e-mail:aakwxk@hotmail.com

CLK A B T NC NC NC NC NC NC NC GND
OE B0 A0 A1 Q0 Q1 Q2 Q3 Q4 Q5 M2 VCC

B0:=B
A0:=A
A1:=A0

Q0:=/A0*A1*B0*/Q0 + A0*/A1*B0*/Q0 + /A0*/A1*Q0 + A0*A1*Q0 + /B0*Q0
Q1:=/A0*A1*B0*/Q0*/Q1 + A0*/A1*B0*Q0*/Q1 + /B0*Q1 + /A0*/A1*Q1 + A0*/Q0*Q1 + A1*Q0*Q1

M2=/A0*A1*B0*/Q0*/Q1 + A0*/A1*B0*Q0*Q1
M2.OE=T
Q2:=Q2*/M2 + M2*/Q2
Q3:=Q3*/M2 + /Q3*M2*A0*Q2 + /Q3*M2*/A0*/Q2 + Q3*M2*A0*/Q2 + Q3*M2*/A0*Q2
Q4:=/A0*M2*/Q2*/Q3*/Q4 + A0*M2*Q2*Q3*/Q4 + /M2*Q4 + /A0*Q3*Q4 + Q2*/Q3*Q4 + A0*/Q2*Q4
Q5:=/A0*M2*/Q2*/Q3*/Q4*/Q5 + A0*M2*Q2*Q3*Q4*/Q5 + /M2*Q5 + /A0*Q4*Q5 + Q3*/Q4*Q5 + A0*/Q3*Q5 + /Q2*Q4*Q5 + /A0*Q2*Q5

DESCRIPTION

input:
A, B => input A,B
CLK => input clock (15ns -> max 67MHz)

output:
Q0,Q1,Q2,Q3 => output, bi-directional counter

temp: (internal use only)
T => (need always set to high)
OE => (need always set to low)
B0 => clock buffered B (dont' connect anything)
A0 => clock buffered A (dont' connect anything)
A1 => clock buffered A0 (dont' connect anything)
M2 => internal (dont' connect anything)

A1:=A0:=A
B0:=B

M=!A1*A0*B0 + A1*!A0*B0

Q0:=Q0*!M + M*!Q0
=> (set in M)
Q0:=Q0*!(!A1*A0*B0 + A1*!A0*B0) + (!A1*A0*B0 + A1*!A0*B0)*!Q0
=> (simplify with BExpred)
Q0:=!A0*A1*B0*!Q0 + A0*!A1*B0*!Q0 + !A0*!A1*Q0 + A0*A1*Q0 + !B0*Q0

Q1:=Q1*!M + !Q1*M*A0*Q0 + !Q1*M*!A0*!Q0 + Q1*M*A0*!Q0 + Q1*M*!A0*Q0
=> (set in M)
Q1:=Q1*!(!A1*A0*B0 + A1*!A0*B0) + !Q1*(!A1*A0*B0 + A1*!A0*B0)*A0*Q0 + !Q1*(!A1*A0*B0 + A1*!A0*B0)*!A0*!Q0 + Q1*(!A1*A0*B0 + A1*!A0*B0)*A0*!Q0 + Q1*(!A1*A0*B0 + A1*!A0*B0)*!A0*Q0
=> (simplify with BExpred)
Q1:=!A0*A1*B0*!Q0*!Q1 + A0*!A1*B0*Q0*!Q1 + !B0*Q1 + !A0*!A1*Q1 + A0*!Q0*Q1 + A1*Q0*Q1

<Note M2 is sync, not clock buffered>
M2=M*(A0*Q0*Q1 + !A0*!Q0*!Q1)
=> (set in M)
M2=(!A1*A0*B0 + A1*!A0*B0)*(A0*Q0*Q1 + !A0*!Q0*!Q1)
=> (simplify with BExpred)
M2=!A0*A1*B0*!Q0*!Q1 + A0*!A1*B0*Q0*Q1

Q2:=Q2*!M2 + M2*!Q2

Q3:=Q3*/M2 + /Q3*M2*A0*Q2 + /Q3*M2*/A0*/Q2 + Q3*M2*A0*/Q2 + Q3*M2*/A0*Q2

Q4:=Q4*!M2 + !Q4*M2*A0*Q2*Q3 + !Q4*M2*!A0*(!Q2*!Q3) + Q4*M2*A0*!(Q2*Q3) + Q4*M2*!A0*(Q2+Q3)
=> (simplify)
Q4:=!A0*M2*!Q2*!Q3*!Q4 + A0*M2*Q2*Q3*!Q4 + !M2*Q4 + !A0*Q3*Q4 + Q2*!Q3*Q4 + A0*!Q2*Q4

Q5:=Q5*!M2 + !Q5*M2*A0*Q2*Q3*Q4 + !Q5*M2*!A0*(!Q2*!Q3*!Q4) + Q5*M2*A0*!(Q2*Q3*Q4) + Q5*M2*!A0*(Q2+Q3+Q4)
=> (simplify)
Q5:=!A0*M2*!Q2*!Q3*!Q4*!Q5 + A0*M2*Q2*Q3*Q4*!Q5 + !M2*Q5 + !A0*Q4*Q5 + Q3*!Q4*Q5 + A0*!Q3*Q5 + !Q2*Q4*Q5 + !A0*Q2*Q5
=> (simplify by abel4)
Q5:=Q3&!Q4&Q5 + !Q2 & Q4 & Q5 +  A0 & !Q3 & Q5 + !A0 & Q2 & Q5 +  !A0 & M2 & !Q2 & !Q3 & !Q4 & !Q5 + A0 & M2 & Q2 & Q3 & Q4 & !Q5 +  !M2 & Q5

