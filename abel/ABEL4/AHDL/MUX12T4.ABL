module Mux12T4 
title '12 to 4 multiplexer   5 June 1990
Dave Pellerin   Data I/O Corp.  Redmond WA'

	mux12t4		device	'P16V8S';

	a0,a1,a2,a3	pin	1,2,3,4;
	b0,b1,b2,b3	pin	5,6,7,8;
	c0,c1,c2,c3	pin	9,11,12,13;
	s1,s0		pin	18,19;
	y0,y1,y2,y3	pin	14,15,16,17;

	H	=	[1,1,1,1];
	L	=	[0,0,0,0];
	X	=	.x.;
	select	=	[s1, s0];
	y 	=	[y3,y2,y1,y0];
	a	=	[a3,a2,a1,a0];
	b	=	[b3,b2,b1,b0];
	c	=	[c3,c2,c1,c0];
	
equations
	when (select == 0) then y = a;
	when (select == 1) then y = b;
	when (select == 2) then y = c;
	when (select == 3) then y = c;
	
test_vectors ([select, a, b, c] -> y)
              [0     , 1, X, X] -> 1; "select = 0, gates lines a to output
	      [0     ,10, H, L] -> 10;
	      [0     , 5, H, L] -> 5;

	      [1     , H, 3, H] -> 3; "select = 1, gates lines b to output
	      [1     ,10, 7, H] -> 7;
	      [1     , L,15, L] -> 15;
		
	      [2     , L, L, 8] -> 8; "select = 2, gates lines c to output
	      [2     , H, H, 9] -> 9;
	      [2     , L, L, 1] -> 1;

	      [3     , H, H, 0] -> 0; "select = 3, gates lines c to output
	      [3     , L, L, 9] -> 9;
	      [3     , H, L, 0] -> 0;

end 
