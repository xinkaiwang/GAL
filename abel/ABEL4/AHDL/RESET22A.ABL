module reset22a
title 'Demonstrates Asynchronous Reset and Synchronous Preset
Tim Charnley    Data I/O Corp   9 Aug 1990'

        reset22a        device  'P22V10';

        Clk,I1,I2,R,S,T Pin 1,2,3,4,5,6;
        Q1,Q2           Pin 14,15 istype 'buffer';

        Ck,Z,H,L        = .C., .Z., 1, 0;
        Input           = [I2,I1];
        Output          = [Q2,Q1];
equations

        Output    := Input;   "Registered buffer

        Output.AR  = R & !T;

        Output.SP  = S & !T;

        Output.Clk = Clk;

test_vectors
       ([Clk,Input,R,S,T] -> Output)
        [ Ck,  0  ,0,0,0] ->   0;
        [ Ck,  1  ,0,0,0] ->   1;
        [ Ck,  2  ,0,0,1] ->   2;
        [ 0 ,  3  ,0,0,1] ->   2;       "Hold
        [ Ck,  3  ,0,0,1] ->   3;

        [ 0 ,  3  ,1,0,1] ->   3;       "Reset = R & !T
        [ 0 ,  3  ,1,0,0] ->   0;       "Async Reset

        [ 0 ,  0  ,0,1,0] ->   0;       "Preset requires clock
        [ Ck,  0  ,0,1,0] ->   3;       "Sync Preset

end

