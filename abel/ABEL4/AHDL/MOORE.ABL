module moore
title 'Moore machine description of a Sequence detector
       Data I/O Corp.  by Jeffrey Davis '

        moore device 'P22V10';

"Inputs
        clk   pin 1;
        PR    pin 2;
        X     pin 3;

"Output
        Q1,Q0,Z   pin 21,22,23 istype 'buffer,reg_D';

"State Register assignment
        sreg    = [Q1,Q0, Z];
        A       = [ 0, 0, 0];"use one bit changes for better optimization
        B       = [ 0, 1, 0];
        C       = [ 1, 1, 0];
        D       = [ 1, 1, 1];

Equations
        sreg.ar  = PR;
        sreg.clk = clk;

state_diagram sreg

state A: 
         if X then B  else A;
                                 
state B: 
         if X then C  else A;

state C: 
         if X then D  else A;

state D:
         if X then D  else A;

test_vectors
        ([clk,PR, X] -> [ sreg ])
         [.c.,1 , 0] -> [   A  ];
         [.c.,0 , 0] -> [   A  ];
         [.c.,0 , 1] -> [   B  ];
         [.c.,0 , 1] -> [   C  ];
         [.c.,0 , 1] -> [   D  ];
         [.c.,0 , 0] -> [   A  ];
         [.c.,0 , 1] -> [   B  ];
         [.c.,0 , 0] -> [   A  ];
         [.c.,0 , 1] -> [   B  ];
         [.c.,0 , 1] -> [   C  ];
         [.c.,0 , 1] -> [   D  ];
         [.c.,0 , 1] -> [   D  ];
         [.c.,0 , 0] -> [   A  ];
         [.c.,0 , 1] -> [   B  ];
         [.c.,0 , 1] -> [   C  ];
         [.c.,0 , 1] -> [   D  ];
end

